/**
* This class represents the Stack Overflow website
*/
class StackOverflow

	types
		
		/** String */
		public String = seq of char;
		
		/** Identifier (start at 1) */ 
		public ID = nat1;


	instance variables
		
		/** List of existing users */
		public users: set of User := {};
		
		/** List of existing questions */
		public questions: seq of Question := [];
		
		-- o.O?
		private static stackoverflow: StackOverflow := new StackOverflow();
		
		/** The user that's currently logged in */
		public loggedUser: [User] := nil;
	
	
	operations
		
		/**
		* Default Constructor
		*
		* @post StackOverflow was created
		*/
		public StackOverflow: () ==> StackOverflow
		StackOverflow() == (
			return self;
		)
		post users = {};
		
		
		/**
		* Creates a new user
		*
		* @param username Username
		* @param password Password
		*
		* @pre  Username is unique
		* @post User was added to users' set
		*/
		public signup: String * String ==> ()
		signup(username, password) == (
			
			-- create user
			dcl user: User := new User(username, password);
			
			-- add user
			users := users union {user};
		)
		pre  (
			forall user in set users & user.username <> username
		)
		post (
			card users = card users~ + 1 and
			exists user in set users & (
				user.username = username and
				user.password = password and
				user.id = User`nextid - 1
		));
		
		
		/**
		* Logs in the user with the given username-password
		*
		* @param username Username
		* @param password Password
		*
		* @pre  There isn't an user already logged in
		* @pre  Username and password are a match
		* @post User logged in
		*/
		public login: String * String ==> ()
		login(username, password) == (
			
			loggedUser := iota user in set users & (user.username = username and user.password = password);
		)
		pre  (
			loggedUser = nil and exists1 user in set users & (
			user.username = username and
			user.password = password
		))
		post (
			loggedUser.username = username and
			loggedUser.password = password
		);
		
		
		/**
		* Logs out the currently logged in user
		*
		* @pre  User must be logged in
		* @post User logged out
		*/
		public logout: () ==> ()
		logout() == (
		
			loggedUser := nil;
		)
		pre  (loggedUser <> nil)
		post (loggedUser = nil);
		
		
		/**
		* Resets this instance
		*
		* @post There are no users and no questions
		*/
		public clear: () ==> ()
		clear() == (
			users := {};
			questions := [];
			loggedUser := nil;
			User`nextid := 1;
			Answer`nextid := 1;
			Question`nextid := 1;
			
		)
		post (users = {} and questions = [] and loggedUser = nil);
		
		
		/**
		* Returns the existent instance
		*/
		public pure static getInstance: () ==> StackOverflow
		getInstance() == (
			return stackoverflow;
		);
		
		
		/**
		* Resets the instance
		*/
		public static clearInstance: () ==> StackOverflow
		clearInstance() == (
			stackoverflow := new StackOverflow();
			return getInstance();
		)
		post RESULT.users = {};


		/**
		* Creates a new question
		*
		* @param user Author
		* @param title Title
		* @param text Text
		*
		* @pre  Author exists
		* @post Question was added to questions' set
		*/
		public addQuestion: String * String ==> Question
		addQuestion(title, text) == (
			
			-- create question
			dcl question: Question := new Question(loggedUser, title, text);
			
			-- add question
			questions := questions^[question];
			
			return question;
		)
		pre  (
			forall question in set elems questions & question.author in set users and
			loggedUser <> nil

		)
		post (
			loggedUser = loggedUser~ and
			len questions = len questions~ + 1 and
			exists question in set elems questions & (
				question.author = loggedUser and
				question.title = title and
				question.text = text
		));
		
		
		/**
		* Shows all questions and their respective answers
		*
		*
		* @pre  At least one question exists
		* @post The user remains logged in and the questions seq remains the same
		*/
		public showQuestionsAndAnswers: () ==> nat
		showQuestionsAndAnswers() == (
		
		 dcl questionsTemp : seq of Question := questions;
		 dcl answersTemp : seq of Answer;
		 dcl questionTemp : Question;
		 dcl answerTemp: Answer;
		 dcl i : nat := 0;
		 dcl j : nat := 0;	 
		 dcl num : nat1 := 1;
		 dcl sum : nat := 0;
		 
		 while i < len questionsTemp do (
		 		i := i + 1;
		 		questionTemp := questionsTemp(i);
		 		
				IO`print("\n");
				IO`print(questionTemp.title);
				IO`print("(");
				IO`print(questionTemp.score);
				IO`print("): \n ");
				IO`print(questionTemp.text);
				IO`print("\n");
				sum := sum + 1;
				
				answersTemp := questionTemp.answers;
				num := 1;
				while j  < len answersTemp do (
				
				j := j + 1;
				answerTemp := answersTemp(j);
				
				IO`print(num);
				IO`print("(");
				IO`print(answerTemp.score);
				IO`print("): ");
				IO`print(answerTemp.text);
				IO`print("\n");
				sum := sum + 1;
				
				num := num + 1;
				)
		 );
		 
		 return sum;
		)
		pre (
			len questions > 0
		)
		post (
			questions = questions~ and
			loggedUser = loggedUser~
		);
		
		/**
		* Upvotes the given answer
		*
		* @param id Answer to upvote
		*
		* @pre  User is logged in
		* @pre  Answer to upvote exists
		* @post No changes were made to other answers
		* @post User stays logged in
		*/
		public upvoteAnswer: ID ==> ()
		upvoteAnswer(id) == (
			
			dcl question: Question := iota q in set elems questions & (id in set {a.id | a in set elems q.answers});
			dcl answer: Answer := iota a in set elems question.answers & a.id = id;
			
			answer.upvote(loggedUser);
		)
		pre  (
			loggedUser <> nil and
			exists q in set elems questions & (
				id in set {a.id | a in set elems q.answers}
			)
		)
		post (
			elems questions \ {q | q in set elems questions & id in set {a.id | a in set elems q.answers}} = elems questions~ \ {q | q in set elems questions~ & id in set {a.id | a in set elems q.answers}} and
			loggedUser = loggedUser~
		);
		
		
		/**
		* Downvotes the given answer
		*
		* @param id Answer to downvote
		*
		* @pre  User is logged in
		* @pre  Answer to downvote exists
		* @post No changes were made to other answers
		* @post User stays logged in
		*/
		public downvoteAnswer: ID ==> ()
		downvoteAnswer(id) == (
		
			dcl question: Question := iota q in set elems questions & (id in set {a.id | a in set elems q.answers});
			dcl answer: Answer := iota a in set elems question.answers & a.id = id;
			
			answer.downvote(loggedUser);
		)
		pre  (
			loggedUser <> nil and
			exists q in set elems questions & (
				id in set {a.id | a in set elems q.answers}
			)
		)
		post (
			elems questions \ {q | q in set elems questions & id in set {a.id | a in set elems q.answers}} = elems questions~ \ {q | q in set elems questions~ & id in set {a.id | a in set elems q.answers}} and
			loggedUser = loggedUser~
		);
		
		
		/**
		* Unvoted the given answer
		*
		* @param id Answer to unvote
		*
		* @pre  User is logged in
		* @pre  Answer to unvote exists
		* @post No changes were made to other answers
		* @post User stays logged in
		*/
		public unvoteAnswer: ID ==> ()
		unvoteAnswer(id) == (
		
			dcl question: Question := iota q in set elems questions & (id in set {a.id | a in set elems q.answers});
			dcl answer: Answer := iota a in set elems question.answers & a.id = id;
			
			answer.unvote(loggedUser);
		)
		pre  (
			loggedUser <> nil and
			exists q in set elems questions & (
				id in set {a.id | a in set elems q.answers}
			)
		)
		post (
			elems questions \ {q | q in set elems questions & id in set {a.id | a in set elems q.answers}} = elems questions~ \ {q | q in set elems questions~ & id in set {a.id | a in set elems q.answers}} and
			loggedUser = loggedUser~
		);
		
		
		/**
		* Upvotes the given question
		*
		* @param id Question to upvote
		*
		* @pre  User is logged in
		* @pre  Question to upvote exists
		* @post No changes were made to other questions
		* @post User stays logged in
		*/
		public upvoteQuestion: ID ==> ()
		upvoteQuestion(id) == (
			
			dcl question: Question := iota q in set elems questions & q.id = id;
			
			question.upvote(loggedUser);
		)
		pre  (
			loggedUser <> nil and
			exists q in set elems questions & (q.id = id)
		)
		post (
			elems questions \ {q | q in set elems questions & q.id = id} = elems questions~ \ {q | q in set elems questions~ & q.id = id} and
			loggedUser = loggedUser~
		);
		
		
		/**
		* Downvotes the given question
		*
		* @param id Question to downvote
		*
		* @pre  User is logged in
		* @pre  Question to downvote exists
		* @post No changes were made to other answers
		* @post User stays logged in
		*/
		public downvoteQuestion: ID ==> ()
		downvoteQuestion(id) == (
		
			dcl question: Question := iota q in set elems questions & q.id = id;
			
			question.downvote(loggedUser);
		)
		pre  (
			loggedUser <> nil and
			exists q in set elems questions & (q.id = id)
		)
		post (
			elems questions \ {q | q in set elems questions & q.id = id} = elems questions~ \ {q | q in set elems questions~ & q.id = id} and
			loggedUser = loggedUser~
		);
		
		
		/**
		* Unvotes the given question
		*
		* @param id Question to unvote
		*
		* @pre  User is logged in
		* @pre  Question to unvote exists
		* @post No changes were made to other questions
		* @post User stays logged in
		*/
		public unvoteQuestion: ID ==> ()
		unvoteQuestion(id) == (
		
			dcl question: Question := iota q in set elems questions & q.id = id;
			
			question.unvote(loggedUser);
		)
		pre  (
			loggedUser <> nil and
			exists q in set elems questions & (q.id = id)
		)
		post (
			elems questions \ {q | q in set elems questions & q.id = id} = elems questions~ \ {q | q in set elems questions~ & q.id = id} and
			loggedUser = loggedUser~
		);

end StackOverflow