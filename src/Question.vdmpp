/**
* This class represents a question
*/
class Question
	
	types
		
		/** String */
		public String = seq of char;
		
		/** Question ID - starts at 1 */
		public ID = nat1;
		
		
	instance variables
		
		/** Id of the next question */
		public static nextid: ID := 1;
		
		/** Id of this question */
		public id: ID;
		
		/** Title */
		public title: String;
		
		/** Text */
		public text: String;
		
		/** List of answers */
		public answers: set of Answer := {};
		
		/** Score */
		public score: int := 0;
		
		/** Set of users that have upvoted this answer */
		public upvoters: set of User := {};
		
		/** Set of users that have downvoted this answer */
		public downvoters: set of User := {};
		
		/** Author */
		public author: [User];

		-- author cant be null
		inv author <> nil;
		
		-- score is the sum of upvotes and downvotes
		inv score = card upvoters - card downvoters;
		
		
	operations
		
		/**
		* Default Constructor
		*
		* @post Question was created
		*/
		public Question: () ==> Question
		Question() == (
		
			author := new User();
			title := " ";
			text := " ";
			id := nextid;
			nextid := nextid + 1;
			return self;
		)
		post (title = " " and text = " " and nextid = id + 1);
		
		/**
		* Constructor
		
		* @param user Original Poster
		* @param String Title
		* @param String Text
		*
		* @pre Author exists
		* @post Question was created
		*/
		public Question: User*String*String ==> Question
		Question(user,title1,text1) == (
			author := user;
			title := title1;
			text := text1;
			id := nextid;
			nextid := nextid + 1;
			return self
		)
		post title = title1 and text = text1 and nextid = id + 1;
		
		
		/**
		* Creates a new Answer
		*
		* @param user Author
		* @param text Text
		*
		* @pre  Author exists
		* @post Answers was added to questions' set
		*/
		public addAnswer: User * String ==> Answer
		addAnswer(user, text) == (
			
			-- create question
			dcl answer: Answer := new Answer(user, text);
			
			-- add question
			answers := answers union {answer};
			
			return answer;
		)
		pre  (
			forall answer in set answers & answer.author in set StackOverflow`getInstance().users
		)
		post (
			card answers = card answers~ + 1 and
			exists answer in set answers & (
				answer.author = user and
				answer.text = text
		));
		
		
		/**
		* Adds an upvote to this question
		*
		* @param user User that upvoted this
		*
		* @pre  User hasn't upvoted this
		* @post User upvoted this
		* @post User hasn't downvoted this
		* @post Score increased by one
		*/
		public upvote: User ==> ()
		upvote(user) == (
			
			atomic (
			
				-- add user to set of voters
				upvoters := upvoters union {user};
				
				-- update score
				score := score + 1;
			);
			
			if (user in set downvoters) then (
			
				atomic (
				
					-- remove user from set of downvoters
					downvoters := downvoters \ {user};
					
					-- update score
					score := score + 1;
				);
	
			);			
		)
		pre  (
			user not in set upvoters
		)
		post (
			upvoters = upvoters~ union {user} and
			downvoters = downvoters~ \ {user} and
			score = card upvoters - card downvoters
		);
		
		
		/**
		* Adds a downvote to this question
		*
		* @param user User that downvoted this
		*
		* @pre  User hasn't downvoted this
		* @post User downvoted this
		* @post User hasn't upvoted this
		* @post Score decreased by one
		*/
		public downvote: User ==> ()
		downvote(user) == (
			
			atomic (
				
				-- add user to set of voters
				downvoters := downvoters union {user};
				
				-- update score
				score := score - 1;
			);
			
			if (user in set upvoters) then (
			
				atomic (
				
					-- remove user from set of upvoters
					upvoters := upvoters \ {user};
				
					-- update score
					score := score - 1;
				)
			);			
		)
		pre  (
			user not in set downvoters
		)
		post (
			downvoters = downvoters~ union {user} and
			upvoters = upvoters~ \ {user} and
			score = card upvoters - card downvoters
		);
		
		
		/**
		* Removes previously casted vote from this
		*
		* @param user User that unvoted this
		*
		* @pre  User has voted on this
		* @post User hasn't upvoted this
		* @post User hasn't downvoted this
		* @post Score updated
		*/
		public unvote: User ==> ()
		unvote(user) == (
			
			if (user in set upvoters) then (
			
				atomic (
				
					-- remove user from set of upvoters
					upvoters := upvoters \ {user};
				
					-- update score
					score := score - 1;
				)
			);
			
			if (user in set downvoters) then (
			
				atomic (
				
					-- remove user from set of downvoters
					downvoters := downvoters \ {user};
				
					-- update score
					score := score + 1;
				)
			);		
		)
		pre  (
			user in set upvoters or user in set downvoters
		)
		post (
			upvoters = upvoters~ \ {user} and
			downvoters = downvoters~ \ {user} and
			score = card upvoters - card downvoters
		);

end Question