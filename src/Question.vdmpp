class Question
	
	types
		
		public String = seq of char;
		
		
	instance variables
		
		public title: seq1 of char := "";
		public text: seq1 of char := "";
		public answers: set of Answer := {};
		public score: int := 0;
		public author: [User];
		
		inv author <> nil;
		
		
	operations
		
		/**
		* Default Constructor
		*
		* @post Question was created
		*/
		public Question: () ==> Question
		Question() == (
		
			author := new User();
			title := " ";
			text := " ";
			return self;
		)
		post (title = " " and text = " ");
		
		/**
		* Constructor
		
		* @param user Original Poster
		* @param String Title
		* @param String Text
		*
		* @pre Author exists
		* @post Question was created
		*/
		public Question: User*String*String ==> Question
		Question(user,title1,text1) == (
			author := user;
			title := title1;
			text := text1;
			return self
		)
		pre user in set StackOverflow`getInstance().users
		post title = title1 and text = text1;
		
		
		/**
		* Creates a new Answer
		*
		* @param user Author
		* @param text Text
		*
		* @pre  Author exists
		* @post Answers was added to questions' set
		*/
		public addAnswer: User * String ==> ()
		addAnswer(user, text) == (
			
			-- create question
			dcl answer: Answer := new Answer(user, text);
			
			-- add question
			answers := answers union {answer};
		)
		pre  (
			forall answer in set answers & answer.author in set StackOverflow`getInstance().users
		)
		post (
			card answers = card answers~ + 1 and
			exists answer in set answers & (
				answer.author = user and
				answer.text = text
		));

end Question